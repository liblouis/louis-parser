table = {
      SOI ~
      ((comment | rule_with_comment | empty_line) ~ NEWLINE)* ~
      EOI
}

comment = _{ wsp_maybe ~ "#" ~ (!NEWLINE ~ ANY)* }
empty_line = _{ wsp_maybe }
rule_with_comment = _{ wsp_maybe ~ rule ~ (end_comment | wsp_maybe) }

wsp = _{ (" " | "\t")+ }
wsp_maybe = _{ (" " | "\t")* }

rule = _{
    // Miscellaneous Opcodes
    include |
    undefined |
    display |
    multind |
    // Character-Definition Opcodes
    space |
    punctuation |
    digit |
    uplow |
    grouping |
    letter |
    lowercase |
    uppercase |
    litdigit |
    sign |
    math |
    // Braille Indicator Opcodes
    capsletter |
    begcapsword |
    endcapsword |
    capsmodechars |
    begcaps |
    endcaps |
    begcapsphrase |
    endcapsphrase |
    lencapsphrase |
    letsign |
    noletsign |
    noletsignbefore |
    noletsignafter |
    nocontractsign |
    numsign |
    numericnocontchars |
    numericmodechars |
    midendnumericmodechars |
    // Opcodes for Standing Alone Sequences
    // Emphasis Opcodes
    emphclass |
    noemphchars |
    emphletter |
    begemphword |
    endemphword |
    emphmodechars |
    begemphphrase |
    endemphphrase |
    lenemphphrase |
    begemph |
    endemph |
    // Computer braille
    // Special Symbol Opcodes
    decpoint |
    hyphen |
    // Special Processing Opcodes
    capsnocont |
    // Translation Opcodes
    compbrl |
    comp6 |
    nocont |
    replace |
    always |
    repeated |
    repword |
    rependword |
    largesign |
    word |
    syllable |
    joinword |
    lowword |
    contraction |
    sufword |
    prfword |
    begword |
    begmidword |
    midword |
    midendword |
    endword |
    partword |
    //exactdots |
    prepunc |
    postpunc |
    begnum |
    midnum |
    endnum |
    joinnum |
    // Character-Class Opcodes
    attribute |
    // Swap Opcodes
    swapcd |
    swapdd |
    swapcc |
    // Context and Multipass Opcodes
    context |
    pass2 |
    pass3 |
    pass4 |
    // The correct Opcode
    correct |
    // The match Opcode
    match_rule |
    pre_pattern |
    post_pattern |

    unknown_rule }

// Character-Definition Opcodes
space = { prefix? ~ "space" ~ wsp ~ unicode_characters ~ wsp ~ dots_with_zero }
punctuation = { prefix? ~ "punctuation" ~ wsp ~ unicode_characters ~ wsp ~ dots_with_zero }
digit = { "digit" ~ wsp ~ unicode_characters ~ wsp ~ dots_with_zero }
uplow = { prefix? ~ "uplow" ~ wsp ~ unicode_characters ~ wsp ~ dots ~ ("," ~ dots)? }
grouping = { "grouping" ~ wsp ~ ASCII_ALPHA+ ~ wsp ~ unicode_character{2} ~ wsp ~ dots ~ "," ~ dots }
letter = { prefix? ~ "letter" ~ wsp ~ unicode_characters ~ wsp ~ dots_with_zero }
lowercase = { prefix? ~ "lowercase" ~ wsp ~ unicode_character ~ wsp ~ dots }
uppercase = { prefix? ~ "uppercase" ~ wsp ~ unicode_character ~ wsp ~ dots }
litdigit = { "litdigit" ~ wsp ~ unicode_character ~ wsp ~ dots }
sign = { prefix? ~ "sign" ~ wsp ~ unicode_character ~ wsp ~ dots_with_zero }
math = { prefix? ~ "math" ~ wsp ~ unicode_character ~ wsp ~ dots_with_zero }
//Braille Indicator Opcodes
capsletter = { "capsletter" ~ wsp ~ dots }
begcapsword = { prefix? ~ "begcapsword" ~ wsp ~ dots  }
endcapsword = { prefix? ~ "endcapsword" ~ wsp ~ dots  }
capsmodechars = { "capsmodechars" ~ wsp ~ unicode_characters }
begcaps = { "begcaps" ~ wsp ~ dots}
endcaps = { "endcaps" ~ wsp ~ dots}
begcapsphrase = { "begcapsphrase" ~ wsp ~ dots}
endcapsphrase = { "endcapsphrase" ~ wsp ~ ("before" | "after") ~ wsp ~ dots}
lencapsphrase = { "lencapsphrase" ~ wsp ~ ASCII_DIGIT }
letsign = { "letsign" ~ wsp ~ dots}
noletsign = { "noletsign" ~ wsp ~ unicode_characters}
noletsignbefore = { "noletsignbefore" ~ wsp ~ unicode_characters}
noletsignafter = { "noletsignafter" ~ wsp ~ unicode_characters}
nocontractsign = { "nocontractsign" ~ wsp ~ dots}
numsign = { "numsign" ~ wsp ~ dots }
numericnocontchars = { "numericnocontchars" ~ wsp ~ unicode_characters}
numericmodechars = { "numericmodechars" ~ wsp ~ unicode_characters}
midendnumericmodechars = { "midendnumericmodechars" ~ wsp ~ unicode_characters}
// Opcodes for Standing Alone Sequences
// Emphasis Opcodes
emphclass = { "emphclass" ~ wsp ~ ascii_characters }
begemph = { "begemph" ~ wsp ~ ascii_characters ~ wsp ~ dots }
endemph = { prefix? ~ "endemph" ~ wsp ~ ascii_characters ~ wsp ~ dots_with_zero }
noemphchars = { "noemphchars" ~ wsp ~ ascii_characters ~ wsp ~ unicode_characters}
emphletter = { "emphletter" ~ wsp ~ ascii_characters ~ wsp ~ dots }
begemphword = { "begemphword" ~ wsp ~ ascii_characters ~ wsp ~ dots }
endemphword = { "endemphword" ~ wsp ~ ascii_characters ~ wsp ~ dots }
emphmodechars = { "emphmodechars" ~ wsp ~ ascii_characters ~ wsp ~ unicode_characters }
begemphphrase = { "begemphphrase" ~ wsp ~ ascii_characters ~ wsp ~ dots }
endemphphrase = { "endemphphrase" ~ wsp ~ ascii_characters ~ wsp ~ ("before" | "after") ~ wsp ~ dots }
lenemphphrase = { "lenemphphrase" ~ wsp ~ ascii_characters ~ wsp ~ ASCII_DIGIT }
// Computer braille
// Special Symbol Opcodes
decpoint = { "decpoint" ~ wsp ~ unicode_characters ~ wsp ~ dots }
hyphen = { "hyphen" ~ wsp ~ unicode_characters ~ wsp ~ dots }
// Special Processing Opcodes
capsnocont = { "capsnocont"}
// Translation Opcodes
compbrl = { "compbrl" ~ wsp ~ unicode_characters }
comp6 = { "comp6" ~ wsp ~ unicode_characters ~ wsp ~ dots}
nocont = { "nocont" ~ wsp ~ unicode_characters}
replace = { "replace" ~ wsp ~ unicode_characters ~ wsp ~ unicode_characters? }
always = { prefix? ~ "always" ~ wsp ~ unicode_characters ~ wsp ~ dots_with_zero  }
repeated = { prefix? ~ "repeated" ~ wsp ~ unicode_characters ~ wsp ~ dots_with_zero  }
repword = { "repword" ~ wsp ~ unicode_characters ~ wsp ~ dots}
rependword = { "rependword" ~ wsp ~ unicode_characters ~ wsp ~ dots ~ "," ~ dots}
largesign = { "largesign" ~ wsp ~ unicode_characters ~ wsp ~ dots }
word = { prefix? ~ "word" ~ wsp ~ unicode_characters ~ wsp ~ dots_with_zero }
syllable = { "syllable" ~ wsp ~ unicode_characters ~ wsp ~ dots}
joinword = { "joinword" ~ wsp ~ unicode_characters ~ wsp ~ dots }
lowword = { "lowword" ~ wsp ~ unicode_characters ~ wsp ~ dots }
contraction = { "contraction" ~ wsp ~ unicode_characters }
sufword = { prefix? ~ "sufword" ~ wsp ~ unicode_characters ~ wsp ~ dots }
prfword = { prefix? ~ "prfword" ~ wsp ~ unicode_characters ~ wsp ~ dots }
begword = { prefix? ~ "begword" ~ wsp ~ unicode_characters ~ wsp ~ dots_with_zero }
begmidword = { prefix? ~ "begmidword" ~ unicode_characters ~ wsp ~ dots }
midword = { prefix? ~ "midword" ~ wsp ~ unicode_characters ~ wsp ~ dots }
midendword = { prefix? ~ "midendword" ~ wsp ~ unicode_characters ~ wsp ~ dots }
endword = { prefix? ~ "endword" ~ wsp ~ unicode_characters ~ wsp ~ dots }
partword = { "partword" ~ wsp ~ unicode_characters ~ wsp ~ dots }
//exactdots = { "exactdots" ~ "@" ~ dots }
prepunc = { prefix? ~ "prepunc" ~ wsp ~ unicode_characters ~ wsp ~ dots }
postpunc = { prefix? ~ "postpunc" ~ wsp ~ unicode_characters ~ wsp ~ dots_with_zero }
begnum = { "begnum" ~ wsp ~ unicode_characters ~ wsp ~ dots }
midnum = { "midnum" ~ wsp ~ unicode_characters ~ wsp ~ dots_with_zero }
endnum = { "endnum" ~ wsp ~ unicode_characters ~ wsp ~ dots_with_zero }
joinnum = { "joinnum" ~ wsp ~ unicode_characters ~ wsp ~ dots }

// Character-Class Opcodes
attribute = { "attribute" ~ wsp ~ (ascii_characters | ASCII_OCT_DIGIT ) ~ wsp ~ unicode_characters }
// Swap Opcodes
swapcd = { "swapcd" ~ wsp ~ ascii_characters ~ wsp ~ dots_with_zero ~ ("," ~ dots)* }
swapdd = { "swapdd" ~ wsp ~ ascii_characters ~ wsp ~ ASCII_NONZERO_DIGIT+ ~ ("," ~ ASCII_NONZERO_DIGIT+)* ~ wsp ~ dots ~ ("," ~ dots)* }
swapcc = { "swapcc" ~ wsp ~ ascii_characters ~ wsp ~ unicode_characters ~ wsp ~ unicode_characters }
// Context and Multipass Opcodes
context = { prefix? ~ "context" ~ wsp ~ multipass_test ~ wsp ~ multipass_action }
pass2 = { prefix? ~ "pass2" ~ wsp ~ multipass_test ~ wsp ~ multipass_action }
pass3 = { prefix? ~ "pass3" ~ wsp ~ multipass_test ~ wsp ~ multipass_action }
pass4 = { prefix? ~ "pass4" ~ wsp ~ multipass_test ~ wsp ~ multipass_action }
// The correct Opcode
 // FIXME: probably not all of prefix is valid below
correct = { prefix? ~ "correct" ~ wsp ~ multipass_test ~ wsp ~ multipass_action }

multipass_test = { unicode_characters } // FIXME
multipass_action = { unicode_characters } // FIXME

// The match Opcode
match_rule = { "match" ~ pre_pattern ~ unicode_characters ~ post_pattern ~ dots }
pre_pattern = { characters } // FIXME
post_pattern = { characters } // FIXME

// Miscellaneous Opcodes
include = { "include" ~ file_characters  }
undefined = { "undefined" ~ dots_with_zero }
display = { "display" ~ unicode_characters ~ dots_with_zero }
multind = { "multind" ~ dots ~ ascii_characters+ }

unknown_rule = { (!NEWLINE ~ ANY)+ }

prefix = { ("noback" | "nofor" | "nocross") ~ wsp }

dot = { ASCII_NONZERO_DIGIT | 'a'..'f' }
dots = @{ "=" | dot+ ~ ("-" ~ dot+)*}

dot_with_zero = { ASCII_DIGIT | 'a'..'f' }
dots_with_zero = @{ dot_with_zero+ ~ ("-" ~ dot_with_zero+)* }

ascii_characters = @{ ASCII_ALPHANUMERIC+ }
file_characters = @{ (ASCII_ALPHANUMERIC | "." | "_" | "-" | "/")+ }
unicode_characters = @{ unicode_character+ }
unicode_character = { hex_char | escape_sequence | LETTER | NUMBER | PUNCTUATION | SYMBOL | MARK | "Â­"}

characters = @{ char+ }
char = { ASCII_ALPHANUMERIC | "." | "_" | "-" | "/" | "`" | "&" | "$" | "@" | hex_char}

escape_sequence = { "\\\\" | "\\f" | "\\n" | "\\r" | "\\s" | "\\t" | "\\v" | "\\e"}

hex_char = { "\\" ~ "x" ~ ASCII_HEX_DIGIT{4} }

end_comment = _{ wsp ~ (!NEWLINE ~ ANY)+ }
